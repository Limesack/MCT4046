<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="UTF-8" />
    <title>LFORBIT</title>
    <style>
      body {
        margin: 0;
      }
    </style>
    <style type="text/css">/*    Removing large graphics for HTML GUI (play button and console)
      html,
      body {
        font-family: Monospace;
        color: #000000;
        background-color: #ffffff;
      }

      #console {
        font-family: Monospace;
        color: #b5b6ff;
        background-color: #000000;
        font-size: 16px;
        width: 805px;
        display: flex;
        align-items: center;
        justify-content: center;
        border-style: solid;
        padding: 20px 0px;
      }

      H1 {
        font-size: 36px;
      }

      #controls {
        width: 100px;
        font-size: 16px;
        text-align:center
      }

      input[type=range] {
        height: 10px;
        -webkit-appearance: none;
        margin: 10px 0;
        width: 100%;
        background: #ffffff;
      }

      input[type=range]:focus {
        outline: none;
      }

      input[type=range]::-webkit-slider-runnable-track {
        width: 100%;
        height: 10px;
        cursor: pointer;
        animate: 0.2s;
        box-shadow: 0px 0px 0px #000000;
        background: #ffffff;
        border-radius: 25px;
        border: 1px solid #000000;
        position: relative;
      }


      #pause {
        margin: auto;
        font-family: Monospace;
        width: 80px;
        padding: 10px 0px;
        font-size: 24px;
        background-color: #b5b6ff;
      }*/
      </style>
    <title>WebAudio Csound</title>
    <script src="js/CsoundObj.js"></script>
    <script type="text/javascript">
      // this will get called when WASM is loaded
      function moduleDidLoad() {
        clear_console();
        console.log = print_msg;
        console.warn = print_msg;
        csound = new CsoundObj();
        csound.setMessageCallback(print_msg);
        csound.compileOrc(document.getElementById('instruments').value);
        //attachListeners();
        window.addEventListener("unload", function(e) {
          if (csound != null)
            csound.destroy();
        }, false);
      }

      // clear console
      function clear_console() {
        var element = document.getElementById('console');
        element.value = ' ';
        document.getElementById('pause').
        addEventListener('click', pause);
      }

      // print to console
      var count = 0;

      function print_msg(message) {
        var element = document.getElementById('console');
        element.value += (message + "\n");
        element.scrollTop = 99999; // focus on bottom
        count += 1;
        if (count == 1000) {
          clear_console();
          count = 0;
        }
      }

      // set parameter
      function SetParam(name, label, scal, off) {
        var val = document.getElementById(name).value / scal + off;
        csound.setControlChannel(name, val);
        console.log(name + ": " + val + " " + label);
      }

      var paused = true;
      var started = false;

      function pause() {
        if (paused == false) {
          csound.stop();
          console.log("pause");
          document.getElementById('pause').
          innerText = 'play';
          paused = true;
        } else {
          if (started == false) {
            CsoundObj.CSOUND_AUDIO_CONTEXT.resume();
            started = true;
          }
          csound.start();
          document.getElementById('pause').
          innerText = 'pause';
          console.log("play");
          paused = false;
        }
      }

      CsoundObj.initialize().then(() => {
        moduleDidLoad();
      });
    </script>
  <link rel="icon" href="favicon.ico"></head>
  <body>
    <canvas></canvas>
      <textarea class="console" cols="55" rows="16" id="console" style="position: absolute; top: 450px; left: 0px">
    loading...</textarea>
      <p>
        <button type="button" id="pause" style="position: absolute; top: 400px; left: 0px"> play </button>
      </p>
      <textarea class="code" id="instruments" hidden>
    //##################################################################################
    // SYNTHESIS CODE
    //  Initialize global variables.
    ksmps = 32
    nchnls = 2
    0dbfs = 1


    // Using schedule instead of the normal score section in CSOUND
    // Defines a instrument number, start time and -1 as the lenght; making the instrument hold a indefine note
    // STARTING THE DIFFERNT INSRUMENTS FOR SYNTHESIS AND PROCESSING
    // Granular Instrument
        schedule 1, 0, -1

    // Spectral Instruments
        schedule 11, 0, -1

    // Noise Percussion
        schedule 21, 0, -1
        schedule 23, 0, -1

    // Guitar pluck
        schedule 41, 0, -1
        schedule 43, 0, -1

    // Bass
        schedule 51, 0, -1
        schedule 53, 0, -1

    // Frequency Noise instrument
        schedule 61, 0, -1

    // Master instrument
        schedule 99, 0, -1

    //##################################################################################
    // INIT GLOBAL VARIABLES
    //##################################################################################
        // GRANULAR
        // classic waveforms for granular synthesis
            giSine ftgen 0, 0, 65537, 10, 1 // sine wave
            giCosine ftgen 0, 0, 8193, 9, 1, 1, 90 // cosine wave
            giTri ftgen 0, 0, 8193, 7, 0, 2048, 1, 4096, -1, 2048, 0 // triangle wave

        // grain envelope tables
            giSigmoRise ftgen 0, 0, 8193, 19, 0.5, 1, 270, 1 // rising sigmoid
            giSigmoFall ftgen 0, 0, 8193, 19, 0.5, 1, 90, 1 // falling sigmoid
            giExpFall ftgen 0, 0, 8193, 5, 1, 8193, 0.00001 // exponential decay
            giTriangleWin ftgen 0, 0, 8193, 7, 0, 4096, 1, 4096, 0 // triangular window

        // Panning table
            giPan		ftgen	0, 0, 32768, -21, 1		//  for panning (random values between 0 and 1)

    //##################################################################################
    // GRANULAR SYNTHESIS INSTRUMENT
    instr 1

        // select source waveform
            kwaveform1 = giSine             // source audio waveform 1
            kwaveform2	= giCosine	      	// source audio waveform 2
        	kwaveform3	= giTri     		// source audio waveform 3
        	kwaveform4	= kwaveform1		// source audio waveform 4

            kwave1Single = 1 // flag to set if waveform is single cycle (set to zero for sampled waveforms)
        	kwave2Single	= kwave1Single
        	kwave3Single	= kwave1Single
            kwave4Single	= kwave1Single

        // get source waveform length (used when calculating transposition and time pointer)
        	kfilen1		tableng	 kwaveform1		// get length of the first source waveform
        	kfilen2		tableng	 kwaveform2		// same as above, for source waveform 2
        	kfilen3		tableng	 kwaveform3		// same as above, for source waveform 3
        	kfilen4		tableng	 kwaveform4		// same as above, for source waveform 4
        	kfildur1	= kfilen1 / sr			// length in seconds, for the first source waveform
        	kfildur2	= kfilen2 / sr			// same as above, for source waveform 2
        	kfildur3	= kfilen3 / sr			// same as above, for source waveform 3
        	kfildur4	= kfilen4 / sr			// same as above, for source waveform 4

        // original pitch for each waveform, use if they should be transposed individually
        // can also be used as a "cycles per second" parameter for single cycle waveforms (assuming that the kwavfreq parameter has a value of 1.0)
        // GRAIN PITCH
            // Fetches Y coordinates and x coordinates from planed 6 and 3 respectively. Used together to control the frequncy of waveform 1 (sine wave)
            kPlanet_6_Y chnget "Planet_6_Y"
            kPlanet_3_X chnget "Planet_3_X"

        	kwavekey1	= kPlanet_6_Y-kPlanet_3_X
        	kwavekey2	= 1
            kwavekey3	= 1
        	kwavekey4	= 1

        // set original key dependant on waveform length (only for sampled waveforms, not for single cycle waves)
        	kwavekey1	= (kwave1Single > 0 ? kwavekey1 : kwavekey1/kfildur1)
        	kwavekey2	= (kwave2Single > 0 ? kwavekey2 : kwavekey2/kfildur2)
        	kwavekey3	= (kwave3Single > 0 ? kwavekey3 : kwavekey3/kfildur3)
        	kwavekey4	= (kwave4Single > 0 ? kwavekey4 : kwavekey4/kfildur4)

        // time pointer (phase). This can be independent for each source waveform.
        	isamplepos1	= 0				// initial phase for wave source 1
        	isamplepos2	= 0				// initial phase for wave source 2
        	isamplepos3	= 0				// initial phase for wave source 3
        	isamplepos4	= 0				// initial phase for wave source 4

        	kTimeRate	= 1				// time pointer rate
        	asamplepos1	phasor kTimeRate / kfildur1	// phasor from 0 to 1, scaled to the length of the first source waveform
        	asamplepos2	phasor kTimeRate / kfildur2	// same as above, scaled for source wave 2
        	asamplepos3	phasor kTimeRate / kfildur3	// same as above, scaled for source wave 3
        	asamplepos4	phasor kTimeRate / kfildur4	// same as above, scaled for source wave 4

        	// mix initial phase and moving phase value (moving phase only for sampled waveforms, single cycle waveforms use static samplepos)
            	asamplepos1	= asamplepos1*(1-kwave1Single) + isamplepos1
            	asamplepos2	= asamplepos2*(1-kwave2Single) + isamplepos2
            	asamplepos3	= asamplepos3*(1-kwave3Single) + isamplepos3
            	asamplepos4	= asamplepos4*(1-kwave4Single) + isamplepos4

        //*******************************
        // granular synthesis parameters
        //*******************************

        // amplitude
        	kamp		= ampdbfs(-5)	// output amplitude for grains
        // sync
        	async 		= 0.0		    // set the sync input to zero (disable external sync)
        // grain rate
            // Fetch x coordinates from planet 4, normalised to a range between 0 and 1; then scaled to modulate the grain rate as desired
            kPlanet_4_X chnget "Planet_4_X"                          // fetch external channel data
            kPlanet_4_XNORM = (kPlanet_4_X-670)/(1012+670)           // normalise to the range [0-1]
            kGrainRate	= (kPlanet_4_XNORM*20)+0.2                   // number of grains per second
        // distribution
            // Fetch y coordinates from planet 4, normalised to a range between 0 and 1; then scaled to modulate the grain distribution as desired
            kPlanet_4_Y chnget "Planet_4_Y"
            kPlanet_4_yNORM = (kPlanet_4_Y-243)/(592.9+243) // Normalised for use in range 0-1
            kdistribution	=	kPlanet_4_yNORM	// grain random distribution in time
            idisttab ftgentmp	0, 0, 16, 16, 1, 16, -10, 0	// probability distribution for random grain masking
        // Panning
            // pans the inividual grains to a random stereo position
            ipan = 0.7
            imid			= .5//  center
            ileftmost		= imid - ipan/2
            irightmost		= imid + ipan/2
            giPanthis		ftgen	0, 0, 32768, -24, giPan, ileftmost, irightmost	//  rescales giPan according to ipan
            			tableiw		0, 0, giPanthis				//  change index 0 ...
            			tableiw		32766, 1, giPanthis			//  ... and 1 for ichannelmasks
            ichannelmasks		= giPanthis		//  ftable for panning
        // grain rate FM
        	kGrFmFreq	= kGrainRate/4				// FM freq for modulating the grainrate
        	kGrFmIndex	= 0.4					// FM index for modulating the grainrate (normally kept in a 0.0 to 1.0 range)
        	iGrFmWave	= giSine				// FM waveform, for modulating the grainrate
        	aGrFmSig	oscil kGrFmIndex, kGrFmFreq, iGrFmWave	// audio signal for frequency modulation of grain rate
        	agrainrate	= kGrainRate + (aGrFmSig*kGrainRate)	// add the modulator signal to the grain rate signal
        // grain shape
            // Fetches x coordinates from planet 5, normalises it to range between [0-1] and scale it as desired to modulate the grain duration, relative to the grain rate
            kPlanet_5_X chnget "Planet_5_X"
            kPlanet_5_XNORM = (kPlanet_5_X-613)/(1062+613)
        	kGrainDur	= kPlanet_5_XNORM					// length of each grain relative to grain rate
        	kduration	= (kGrainDur*2500)/kGrainRate		// grain dur, relative to grain rate

        	ienv_attack	= giSigmoRise 				// grain attack shape (from table)
        	ienv_decay	= giSigmoFall 				// grain decay shape (from table)
        	ksustain_amount	= 0.25					// balance between enveloped time(attack+decay) and sustain level time, 0.0 = no time at sustain level
        	ka_d_ratio	= 0.5                       // balance between attack time and decay time, 0.0 = zero attack time and full decay time

        	kenv2amt	= 0.0				 	    // amount of secondary enveloping per grain (e.g. for fof synthesis)
        	ienv2tab	= giExpFall 				// secondary grain shape (from table), enveloping the whole grain if used
        // grain pitch (transpose, or "playback speed")
            //transposition
            // transposes the individual grains up or down in the range 1200 to -1200 cents from the base frequency set kwavekey
                kcent = 0 // transposition
                kcentrand		rand 1200	//  random transposition
                iorig			= 1       	//  original pitch
                kwavfreq		= iorig * cent(kcent + kcentrand) // transposition factor (playback speed) of audio inside grains,
        // pitch sweep
        	ksweepshape		= 0.5						// grain wave pitch sweep shape (sweep speed), 0.5 is linear sweep
        	iwavfreqstarttab 	ftgentmp	0, 0, 16, -2, 0, 0,   1		// start freq scalers, per grain
        	iwavfreqendtab		ftgentmp	0, 0, 16, -2, 0, 0,   1		// end freq scalers, per grain
        // FM of grain pitch (playback speed)
        	kPtchFmFreq	= 440							// FM freq, modulating waveform pitch
        	kPtchFmIndex	= 0							// FM index, modulating waveform pitch
        	iPtchFmWave	= giSine						// FM waveform, modulating waveform pitch
        	ifmamptab 	ftgentmp	0, 0, 16, -2, 0, 0,   1			// FM index scalers, per grain
        	ifmenv		= giTriangleWin 					// FM index envelope, over each grain (from table)
        	kPtchFmIndex	= kPtchFmIndex + (kPtchFmIndex*kPtchFmFreq*0.00001) 	// FM index scaling formula
        	awavfm		oscil	kPtchFmIndex, kPtchFmFreq, iPtchFmWave		// Modulator signal for frequency modulation inside grain
        // trainlet parameters
        	icosine		= giCosine				// needs to be a cosine wave to create trainlets
        	kTrainCps	= kGrainRate				// set cps equal to grain freq, creating a single cycle of a trainlet inside each grain
        	knumpartials	= 7					// number of partials in trainlet
        	kchroma		= 3					// chroma, falloff of partial amplitude towards sr/2

        // masking
        	// gain masking table, amplitude for individual grains
        	   igainmasks 	ftgentmp	0, 0, 16, -2, 0, 0,   1

        	// random masking (muting) of individual grains
                // uses the x coordinates from planet 2, in a scaled form to modulate the muting of random grains
                kPlanet_2_X chnget "Planet_2_X"
        	    krandommask	= kPlanet_2_X/5000
        	// wave mix masking.
        	// Set gain per source waveform per grain,
        	// in groups of 5 amp values, reflecting source1, source2, source3, source4, and the 5th slot is for trainlet amplitude.
        	    iwaveamptab	ftgentmp	0, 0, 32, -2, 0, 0,   1,1,0,0,1

        // system parameter
            imax_grains	= 100				// max number of grains per k-period

        // GRANULAR SYNTHESIS
            aGrain_Out_L,aGrain_Out_R,a3,a4,a5,a6,a7,a8 partikkel \ // (beginner)
            kGrainRate, \ // grains per second *
            kdistribution, idisttab, async, \ // synchronous/asynchronous
            kenv2amt, ienv2tab, ienv_attack, ienv_decay, \ // grain envelope (advanced)
            ksustain_amount, ka_d_ratio, kduration, \ // grain envelope *
            kamp, \ // amp *
            igainmasks, \ // gain masks (advanced)
            kwavfreq, \ // grain pitch (playback frequency) *
            ksweepshape, iwavfreqstarttab, iwavfreqendtab, \ // grain pith sweeps (advanced)
            awavfm, ifmamptab, ifmenv, \ // grain pitch FM (advanced)
            icosine, kTrainCps, knumpartials, kchroma, \ // trainlets
            ichannelmasks, \ // channel mask (advanced)
            krandommask, \ // random masking of single grains *
            kwaveform1, kwaveform2, kwaveform3, kwaveform4, \ // set source waveforms * (using only waveform 1)
            iwaveamptab, \ // mix source waveforms
            asamplepos1, asamplepos2, asamplepos3, asamplepos4, \ // read position for source waves * (using only samplepos 1)
            kwavekey1, kwavekey2, kwavekey3, kwavekey4, \ // individual transpose for each source
            imax_grains // system parameter (advanced)

        // Summing and scaling outputs from partikkel
            aSumL = (aGrain_Out_L+a3+a7)/4
            aSumR = (aGrain_Out_R+a4+a8)/4

        // Reverb
            kfblvl = kPlanet_5_XNORM+0.70
            kfco = 9500
            aRevL, aRevR reverbsc aSumL, aSumR, kfblvl, kfco
            // fetches x coordinates from planet 6 to controll the wet-dry reverb blend
            kPlanet_6_X chnget "Planet_6_X"
            kPlanet_6_XNORM = (kPlanet_6_X-563)/(1112+563) // Normalised for use in range 0-1
        // Sums dry and reverated signals, and send the result to the master instrument
            aoutL = aSumL + aRevL*(kPlanet_6_XNORM+0.20)
            aoutR = aSumR + aRevR*(kPlanet_6_XNORM+0.20)
        // OUTPUT
            chnset aoutL, "Grain_Out_L"
            chnset aoutR, "Grain_Out_R"

      endin
    //##################################################################################
    // SPECTRAL PROCESSING
    instr 11
        // Initialize EMPTY DELAY LINE
            aGrainPitchedL init 0
            aGrainPitchedR init 0

        // AUDIO IN FOR SPECTRAL PROCCESING
            aPulse_Out_L chnget "Pulse_Out_L"
            aPulse_Out_R chnget "Pulse_Out_R"
            aGrain_Out_L chnget "Grain_Out_L"
            aGrain_Out_R chnget "Grain_Out_R"

        // FEEDBACK FROM PITCH SCALING ADDED TO DRY SIGNAL
            kskal = 0.75 // FEEDBACK SCALING
            aGrain_Out_L += aGrainPitchedL*kskal
            aGrain_Out_R += aGrainPitchedR*kskal

        // WINDOW DEFINITION
            ifftsize 	= 8192 // The FFT size in samples.
            ioverlap	= ifftsize/16 // distance in samples between overlapping analysis frames
            iwinsize	= ifftsize	// size in samples of the analysis window filter
            iwintype 	= 0	// shape of the analysis window

        // FFT-analysis of the different input channels
            fGrainL 		pvsanal aGrain_Out_L, ifftsize, ioverlap, iwinsize, iwintype
            fGrainR 		pvsanal aGrain_Out_R, ifftsize, ioverlap, iwinsize, iwintype
            fPulseL 		pvsanal aPulse_Out_L, ifftsize, ioverlap, iwinsize, iwintype
            fPulseR 		pvsanal aPulse_Out_R, ifftsize, ioverlap, iwinsize, iwintype

        // PVS SCALING
        	kscal = 2 // scaling ratio.
        	kkeepform = 0 // attempt to keep input signal formants
        	kgain = 1 // amplitude scaling (defaults to 1).
        	kcoefs = 80 // number of cepstrum coefs used in formant preservation (defaults to 80).
            fGrainPitchedL pvscale fGrainL, kscal, kkeepform, kgain, kcoefs
            fGrainPitchedR pvscale fGrainR, kscal, kkeepform, kgain, kcoefs

        // PITCHED SIGNAL RESYNTHESIS
            aGrainPitchedL  	pvsynth   fGrainPitchedL
            aGrainPitchedR  	pvsynth   fGrainPitchedR
        // DELAYED TO CREATE A PITCHED DELAY
            aGrainPitchedL delay aGrainPitchedL, 0.2 // delay in seconds
            aGrainPitchedR delay aGrainPitchedR, 0.2 // delay in seconds

        // PVS morph
            kPlanet_7_X chnget "Planet_7_X"
            kPlanet_7_XNORM = (kPlanet_7_X-513)/(1162+513) // Normalised for use in range 0-1
            kPlanet_2_X chnget "Planet_2_X"
            kPlanet_2_XNORM = (kPlanet_2_X-748)/(928+748) // Normalised for use in range 0-1

        	kampint = kPlanet_2_XNORM // Amplitude interpolation between input 1 and 2
        	kfrqint = kPlanet_7_XNORM+0.5 // Frequency interpolation between input 1 and 2
            fProccesedinL pvsmorph fGrainL, fPulseL, kampint, kfrqint
            fProccesedinR pvsmorph fGrainR, fPulseR, kampint, kfrqint

        // FREEZE
            // fetches x coordinates from planet 8, normalises it and uses it to modulate the frequency of freezing, and controll the freeze time
            kPlanet_8_X chnget "Planet_8_X"
            kPlanet_8_XNORM = (kPlanet_8_X-463)/(1212+463) // Normalised for use in range 0-1
            kmin = 0.7
            kmax = 1-kPlanet_8_XNORM                       // Sets the freeze time as the inverse of the scaled value, i.e. when frequency is low, the freeze time is increased
            kFrequency = kPlanet_8_XNORM/2
            kfreq       randomh kmin, kmax, kFrequency // probability of freezing freq
        	kamp		randomh	kmin, kmax, kFrequency // probability of freezing amplitudes
        	fFreezedL		pvsfreeze	fProccesedinL, kamp, kfreq // freeze amps or freqs independently
        	fFreezedR		pvsfreeze	fProccesedinR, kamp, kfreq // freeze amps or freqs independently


        // SMOOTH
        	iframetime	= (ioverlap/sr) // time per fft frame rate
        	ktimeAmp	= 0.8 // smoothing of amplitude
        	ktimeFreq	= 0.1 // smoothing of frequency
        	kamountAmp	= iframetime/ktimeAmp					// smoothing frequency (in fraction of 1/2 fft framerate)
        	kamountFreq	= iframetime/ktimeFreq					// smoothing frequency (in fraction of 1/2 fft framerate)
            fProccesedoutL		pvsmooth fFreezedL, kamountAmp, kamountFreq
            fProccesedoutR		pvsmooth fFreezedR, kamountAmp, kamountFreq

          // resynthesis of pvs streams
              aProccesedL  	pvsynth   fProccesedoutL
              aProccesedR  	pvsynth   fProccesedoutR
          // Audio out to master instrument
              aoutL		= aProccesedL
              aoutR		= aProccesedR

              chnset aoutL, "Spectral_Out_L"
              chnset aoutR, "Spectral_Out_R"

      endin
    //#################################################################################
    //SYNTH CONTROLLER
    instr 21
        // Collect controll signals from HTML
            kPlanet_2_X chnget "Planet_2_X"
            kPlanet_2_XNORM = (kPlanet_2_X-748)/(927+748) // Normalised for use in range 0-1

        //Metronome
            kmetroRandom random 0.1, kPlanet_2_XNORM+0.20
            ktrigger metro kmetroRandom

            kmintim = 0 // minimum time between generated events, in seconds.
            kmaxnum = 2 // maximum number of simultaneous instances of instrument kinsnum allowed.
            kinsnum = 22 //instrument number
            kwhen = 0 // start time of the new event. Equivalent to p2 in a score i statement.
            kDuration = 0.25 //  duration of event.

        //Frequency-array
            kArray[] fillarray 440, 523.25, 659.25, 698.46
            kRandom random 0, 4

        //Parameters
            kAmplitude random 0.5, 1
            kFrequency = kArray[kRandom]

        schedkwhen ktrigger, kmintim, kmaxnum, kinsnum, kwhen, kDuration, kDuration, kAmplitude, kFrequency, ktrigger

      endin
    // synthesis
    instr 22
        // Synth controlls from p-fields
            kAmplitude = p5 // amplitude
            kFrequency = p6 // in Hz
            ktrigger = p7

        //Karplus-Strong opcode for randomized drum sound
            kEnv madsr .01,0.5,0.3,0.1 //Envelope
            aOut pluck kAmplitude, randomh:k(2,10,10), 220, 0, 1, .1, 10 //

        //Random Pan
            ktrigon trigger ktrigger, 0.5, 0
            kPan trandom ktrigon, 0.2, 0.8

        // output and panning
            aOut  *=     sqrt((1 - kPan))*kEnv
            aOut  *=     sqrt(kPan)*kEnv
        //audio out
            chnset aOut, "SynthL"
            chnset aOut, "SynthR"
      endin
    // Synth reverb and ouput
    instr 23
        // audio in
            aL chnget "SynthL"
            aR chnget "SynthR"

            kPlanet_3_X chnget "Planet_3_X"
            kPlanet_3_XNORM = (kPlanet_3_X-713)/(957+713) // Normalised for use in range 0-1

        //Filters L+R
            afiltL moogvcf aL, 1500 , 0.3
            afiltR moogvcf aR, 1500 , 0.3

        //Reverb
            arevl, arevr reverbsc afiltL, afiltR, kPlanet_3_XNORM+0.8, 8000
        //Mix dry + wet
            aoutL = afiltL + arevl
            aoutR = afiltR + arevr
        //Audio out
            chnset aoutL, "Pulse_Out_L"
            chnset aoutR, "Pulse_Out_R"
      endin
    //##################################################################################
    // GUITAR PLUCK
    instr 41
        // SYNTH CONTROLLER

        // Metronome
            kmetroRandom random 0.07, 0.14
            ktrigger metro kmetroRandom

            kmintim = 0 // minimum time between generated events, in seconds.
            kmaxnum = 2 // maximum number of simultaneous instances of instrument kinsnum allowed.
            kinsnum = 42 //instrument number
            kwhen = 0 // start time of the new event.
            kDuration = 0.25 // duration of event.

        // Frequency-array
            kArray[] fillarray 783.99, 1174.66, 1318.51, 1567.98
            kRandom random 0, 4

        // Parameters
            kAmplitude random 0.75, 1
            kFrequency = kArray[kRandom]

            schedkwhen ktrigger, kmintim, kmaxnum, kinsnum, kwhen, kDuration, kDuration, kAmplitude, kFrequency, ktrigger

      endin
    instr 42

        kAmplitude = p5 // amplitude
        kFrequency = p6 // in Hz
        ktrigger = p7

        //Karplus-Strong opcode for plucked string sound
        kEnv madsr 0.1, .1, .2, .1
        aOut pluck kAmplitude, kFrequency, 50, 0, 1, .5, 10

        //Random Pan
            ktrigon trigger ktrigger, 0.5, 0
            kPan trandom ktrigon, 0.2, 0.8

            aSigL  =     aOut * sqrt((1 - kPan))
            aSigR  =     aOut * sqrt(kPan)

        //Adds envelope
            aSigL = aSigL * kEnv
            aSigR = aSigR * kEnv

        //Audio out
            chnset aSigL, "Guitar_L"
            chnset aSigR, "Guitar_R"

      endin
    instr 43
        //Audio in
            aSigL chnget "Guitar_L"
            aSigR chnget "Guitar_R"

        //Filters L+R
            afiltL moogvcf aSigL, 1500, 0.5
            afiltR moogvcf aSigR, 1500, 0.5
        //Reverb
            arevl, arevr reverbsc afiltL, afiltR, 0.94, 8000
        //Mix dry+wet
            aoutL = afiltL + arevl
            aoutR = afiltR + arevr

        //Audio out
            chnset aoutL, "GIT_L"
            chnset aoutR, "GIT_R"

      endin
    //##################################################################################
    // Bass_Synth
    instr 51
        //SYNTH CONTROLLER

        //Randomized trigger
            kmetro random 0.03, 0.08
            ktrigger metro kmetro

            ktrigger = ktrigger // triggers a new score event.
            kmintim = 0 // minimum time between generated events, in seconds.
            kmaxnum = 2 // maximum number of simultaneous instances of instrument kinsnum allowed.
            kinsnum = 52 //instrument number
            kwhen = 0 // start time of the new event.
            kDuration = 10 // duration of event.

            //                                                                p4
            schedkwhen ktrigger, kmintim, kmaxnum, kinsnum, kwhen, kDuration, kDuration

      endin
    instr 52

            koffset = 0.12 // offset
            kAmplitude = 0.45 // amplitude
            kPlanet_3_Y chnget "Planet_3_Y"
            kFrequency = (kPlanet_3_Y+40)/10 // in Hz
            imode = 0 // 0: sawtooth | 2: square/PWM | 4: sawtooth/triangle/ramp
            kpw = 0 // the pulse width of the square wave (imode waveform=2) or the ramp characteristics of the triangle wave (imode waveform=4).

            aVCO1 vco2 kAmplitude, kFrequency, imode, kpw
            aVCO2 vco2 kAmplitude, kFrequency+koffset, imode, kpw

        //SUM FROM OSCILATORS
            aOut = (aVCO1+aVCO2)*0.5

        // FILTER *** HENTE FRA HTLM
            kcf = 120 // filter cutoff frequency
            kres = 0.5 // resonance, generally < 1, but not limited to it.

            aOut moogladder2 aOut, kcf, kres

        // Envelope
            iatt = p4*0.1 //0.05// duration of attack phase
            idec = p4*0.2 //0// duration of decay
            islev = 1 // level for sustain phase
            irel = p4*0.7 //0.1// duration of release phase

            kEnvelope adsr iatt, idec, islev, irel

            chnset (aOut*2)*kEnvelope, "Bass_Synth"

      endin
    //##################################################################################
    // Noise
    instr 61

            kdensity random 100, 200 //Density between each noise grain
            kcf_1 = 150 //Filter cutoff
            kcf_2 = 100 //Filter cutoff

            aNoise_1 pinker //Generates noise
            aNoise_1_Filt moogvcf aNoise_1, kcf_1, 0.5 //Filter

            aNoise_2  dust2 0.5, kdensity //Generates noise by creating a series of impulses depending on kdensity (100-200 per second)
            aNoise_2_Filt tone aNoise_2, kcf_2 //Filter

            asum = aNoise_1_Filt + aNoise_2_Filt //Mix L+R

            chnset asum, "LF_noise" //Audio out

        // high frequency Noise
            // Creates pink noise
                aHFnoise pinker
            // X coordinates from planet 4 controlls cut offs for both lowpass and highpass filters
                kPlanet_4_X chnget "Planet_4_X"
                kFrequency = kPlanet_4_X
                aHFnoise butterhp aHFnoise, kFrequency
                aHFnoise butterlp aHFnoise, kFrequency*1.5

            // Fetches and scales the x coordinates from planet 8 to control the dry/wet reverb ratio of the High frequent noise
                kPlanet_8_X chnget "Planet_8_X"
                kPlanet_8_XNORM = (kPlanet_8_X-463)/(1212+463)
                kPlanet_8_XNORM portk kPlanet_8_XNORM, 0.05
                // Scales the High frequent noise with the normalised value
                aHFnoise *= kPlanet_8_XNORM+0.30
            //Reverb
                kfblvl = 0.96
                kfco = 6000
                aHFnoiseL ,aHFnoiseR reverbsc aHFnoise, aHFnoise, kfblvl, kfco

            // Scaling output and sending it to master instrument
                kskal = 0.08
                aHFnoiseL *= kskal
                aHFnoiseR *= kskal

                chnset aHFnoiseL, "HFnoiseL"
                chnset aHFnoiseR, "HFnoiseR"

      endin
    //##################################################################################
    // MASTER OUT INSTRUMENT
    instr 99
        // Collect dry from granular instrument
            aGrain_Out_L chnget "Grain_Out_L"
            aGrain_Out_R chnget "Grain_Out_R"

        // Collect from Spectral prossecing
            aSpectral_Out_L chnget "Spectral_Out_L"
            aSpectral_Out_R chnget "Spectral_Out_R"

        // Collect from Pulse instruments
            aPulse_Out_L chnget "Pulse_Out_L"
            aPulse_Out_R chnget "Pulse_Out_R"

        // Collect from Git simulator
            aGIT_L chnget "GIT_L"
            aGIT_R chnget "GIT_R"

        // Collect from Bass SYNTH
            aBass chnget "Bass_Synth"

        // Collect from Frequency noise generator
            aLFnoise chnget "LF_noise"
            aHFnoiseL chnget "HFnoiseL"
            aHFnoiseR chnget "HFnoiseR"

        // MAIN OUTPUT
        // Summing all the individual channels (gained in their respective instruments) and scales the total value
            kGlobal_Out_skal = 0.5
            outs (aPulse_Out_L+aGIT_L+aBass+aGrain_Out_L+aLFnoise+aHFnoiseL+aSpectral_Out_L)*kGlobal_Out_skal,\
                 (aPulse_Out_R+aGIT_R+aBass+aGrain_Out_R+aLFnoise+aHFnoiseR+aSpectral_Out_R)*kGlobal_Out_skal

      endin
    //##################################################################################
    </textarea>
    <script src="./js/canvas.bundle.js"></script></body>
     <script src="js/canvas.js"></script>

     <script>
     // Animation Loop
     function animate() {
       requestAnimationFrame(animate);
       c.clearRect(0, 0, canvas.width, canvas.height);
       c.fillStyle = 'rgb(0, 0, 0)';
       c.fillRect(0, 0, canvas.width, canvas.height);

       stars.forEach(star => {
         star.draw();
         // Code can be increased here to animate, or interact with the stars in the system
       });

       planets.forEach(planet => {
         planet.update();
         // Importing planet parametres from Canvas and sends them to CSound instance
         if(typeof csound != "undefined"){
          csound.setControlChannel("Planet_0_X", planets[0].x);
         csound.setControlChannel("Planet_0_Y", planets[0].y);
         csound.setControlChannel("Planet_0_Radian", planets[0].radian);
         csound.setControlChannel("Planet_1_X", planets[1].x);
         csound.setControlChannel("Planet_1_Y", planets[1].y);
         csound.setControlChannel("Planet_1_Radian", planets[1].radian);
         csound.setControlChannel("Planet_2_X", planets[2].x);
         csound.setControlChannel("Planet_2_Y", planets[2].y);
         csound.setControlChannel("Planet_2_Radian", planets[2].radian);
         csound.setControlChannel("Planet_3_X", planets[3].x);
         csound.setControlChannel("Planet_3_Y", planets[3].y);
         csound.setControlChannel("Planet_3_Radian", planets[3].radian);
         csound.setControlChannel("Planet_4_X", planets[4].x);
         csound.setControlChannel("Planet_4_Y", planets[4].y);
         csound.setControlChannel("Planet_4_Radian", planets[4].radian);
         csound.setControlChannel("Planet_5_X", planets[5].x);
         csound.setControlChannel("Planet_5_Y", planets[5].y);
         csound.setControlChannel("Planet_5_Radian", planets[5].radian);
         csound.setControlChannel("Planet_6_X", planets[6].x);
         csound.setControlChannel("Planet_6_Y", planets[6].y);
         csound.setControlChannel("Planet_6_Radian", planets[6].radian);
         csound.setControlChannel("Planet_7_X", planets[7].x);
         csound.setControlChannel("Planet_7_Y", planets[7].y);
         csound.setControlChannel("Planet_7_Radian", planets[7].radian);
         csound.setControlChannel("Planet_8_X", planets[8].x);
         csound.setControlChannel("Planet_8_Y", planets[8].y);
         csound.setControlChannel("Planet_8_Radian", planets[8].radian);
         }
       });
     }
     planetToDrag = null
          	dragStart = [0,0]
          	dragEnd = [0,0]
          	dragging = false

          	function getCursorPosition(canvas, event) {
     		   const rect = canvas.getBoundingClientRect()
     		   const x = event.clientX - rect.left
     		   const y = event.clientY - rect.top
     		   return [x,y]

     		}
     		canvas.addEventListener('mousedown', function(e) {
     		    dragStart = getCursorPosition(canvas, e)
     		    dragging = true;
             mouseRadius =  Math.sqrt((dragStart[0]-canvas.width / 2)**2+(dragStart[1]-canvas.height / 2)**2)

             smallesRadiusDiff = 99999
             planets.forEach(planet => {
               if (Math.abs(planet.orbitRadius-mouseRadius)<smallesRadiusDiff){
                 planetToDrag = planet
                 smallesRadiusDiff = Math.abs(planet.orbitRadius-mouseRadius)
               }
             })

     		})
     		canvas.addEventListener('mouseup', function(e) {
     		    dragging = false;
     		})
     		canvas.addEventListener('mousemove', function(e) {
     			if (dragging){
     				dragEnd = getCursorPosition(canvas, e)
     				dist = (dragEnd[0]-dragStart[0])/10000
             planetToDrag.velocity = Math.max(planetToDrag.velocity + dist, 0.0001)
             dragStart = dragEnd;
     			}
     		})
     animate()
   </script>
</html>
